(*
 * A Grako grammar for TeX language.
   Because whitespace is significant in TeX language,
   all rule names start with an uppercase letter so whitespace
   is not skipped when invoking them.
 *)
@@grammar :: TeXToken
@@eol_comments :: /%.*?$/

start               = DOC $ ;
DOC                 = { ELEMENT }* ;
ELEMENT             = GROUP | CMD | MARKER | TEXT ;
GROUP               = '{' { ELEMENT }+ '}' ;
CMD                 = MACRO_DEF_CMD | CONTROL_CMD | SYMBOL_CMD ;
MACRO_DEF_CMD       = LATEX_MACRO_DEF_CMD | TEX_MACRO_DEF_CMD ;
LATEX_MACRO_DEF_CMD = '\\newcommand' ARG ([ OPT ]) ARG ;
TEX_MACRO_DEF_CMD   = '\\def' '\' { /[a-zA-Z]/ }+ { MARKER }* ARG ;
CONTROL_CMD         = '\' { /[a-zA-Z]/ }+ { ARG | OPT }* ;
SYMBOL_CMD          = '\' { /[^a-zA-Z0-9]/ }+ ( ARG | OPT | /[a-zA-Z]/ ) ;
ARG                 = '{' { ELEMENT }* '}' ;
OPT                 = '[' { ELEMENT }* ']' ;
MARKER              = '#' /[0-9]/ ;
TEXT                = WHITESPACE | WORD ;
WHITESPACE          = NEW_PARAGRAPH | NEW_LINE | SPACE ;
NEW_PARAGRAPH       = ( /\n/ | /\r\n/ ) { /\n/ | /\r\n/ }+ ;
NEW_LINE            = /\n/ | /\r\n/ ;
SPACE               = { ' ' }+ ;
WORD                = { /[^\\\#\{\}\[\]\r\n\s]/ }+ ;
